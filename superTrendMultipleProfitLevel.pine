//@version=5
strategy("SuperTrend with multiple Profit booking.", overlay=true, default_qty_type=strategy.percent_of_equity, initial_capital=7000, default_qty_value=95, commission_type=strategy.commission.percent, commission_value=0.1)

atrPeriod = input.int(10, "ATR Length")
factor = input.int(3, "Factor")
candleDivider = input.float(0.003, "Candle Height", step=0.0001)

[supertrend, direction] = ta.supertrend(factor, atrPeriod)
plot(direction < 0 ? supertrend : na, "Up Trend", color = color.green, style=plot.style_linebr)
plot(direction < 0? na : supertrend, "Down Trend", color = color.red, style=plot.style_linebr)

isUptrend = direction < 0
isDowntrend = direction > 0

var bool isTradedLong = false
var bool isTradedShort = false
var float maxPainLong = 0
var float maxPainShort = 0
var int maxPainLongBars = 0
var int maxPainShortBars = 0

longCondition = isUptrend and isTradedLong != true

if(strategy.position_size > 0)
    if(maxPainLong == 0 or maxPainLong > low)
        maxPainLong := low
        maxPainLongBars := 0
    else
        maxPainLongBars := maxPainLongBars + 1

if(strategy.position_size < 0)
    if(maxPainShort == 0 or maxPainShort < high)
        maxPainShort := high
        maxPainShortBars := 0
    else
        maxPainShortBars := maxPainShortBars + 1

percentAsPoints(pcnt) =>
    strategy.position_size != 0 ? math.round(pcnt / 100 * strategy.position_avg_price / syminfo.mintick) : float(na)

if (longCondition)
    strategy.entry("Long " + "Max Pain Short: " + str.tostring(maxPainShort) + " Max Pain Short Bars: " + str.tostring(maxPainShortBars), strategy.long)
    isTradedLong := true
    isTradedShort := false
    maxPainLong := 0
    maxPainLongBars := 0

shortCondition = isDowntrend and isTradedShort != true

if (shortCondition)
    strategy.entry("Short " + "Max Pain Long: " + str.tostring(maxPainLong) + " Max Pain Long Bars: " + str.tostring(maxPainLongBars), strategy.short)
    isTradedShort := true
    isTradedLong := false
    maxPainShort := 0
    maxPainShortBars := 0

strategy.exit("1st Target 50 %", qty_percent = 30, profit = percentAsPoints(10))
strategy.exit("2nd Target 90 %", qty_percent = 60, profit = percentAsPoints(30))
strategy.exit("3rd Target 100 %", profit = percentAsPoints(40))

if((strategy.position_size > 0 and low <= (strategy.position_avg_price * 0.90)) or (strategy.position_size < 0 and high >= (strategy.position_avg_price * 1.1)))
    strategy.cancel("1st Target 50 %")
    strategy.cancel("2nd Target 90 %")
    strategy.cancel("3rd Target 100 %")
    strategy.close_all(comment = "Stop Loss")

profitPercent(price) =>
    posSign = strategy.position_size > 0 ? 1 : strategy.position_size < 0 ? -1 : 0
    (price - strategy.position_avg_price) / strategy.position_avg_price * posSign * 100

p1 = plot(profitPercent(high), style=plot.style_linebr, title = "open profit % upper bound")
p2 = plot(profitPercent(low), style=plot.style_linebr, title = "open profit % lower bound")
fill(p1, p2, color = color.red)